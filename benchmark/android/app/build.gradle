apply plugin: 'com.android.application'

android {
    compileSdkVersion androidVersions.compileSdkVersion
    ndkVersion androidSdkVersions.ndkVersion
    buildToolsVersion androidSdkVersions.buildToolsVersion

    defaultConfig {
        applicationId = 'com.mapbox.mapboxsdk.maps.benchmark_runner'
        minSdkVersion 21
        targetSdkVersion 33
        def abi = 'all'
        if (project.hasProperty('mapbox.abis')) {
            abi = project.getProperty('mapbox.abis')
        }
        ndk {
            if (abi != 'all') {
                abiFilters abi.split(' ')
            } else {
                abiFilters 'armeabi-v7a', 'x86', 'arm64-v8a', 'x86_64'
            }
        }
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_CCACHE=ccache'
                arguments '-DANDROID_STL=c++_static'
                targets 'mbgl-benchmark-runner'
            }
        }
        android {
            testBuildType 'release'
        }
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    externalNativeBuild {
        cmake {
            version androidSdkVersions.cmakeVersion
            path '../../../CMakeLists.txt'
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.debug
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation 'androidx.test:rules:1.5.0'
}
