load("@rules_cc//cc:defs.bzl", "cc_library", "objc_library")
load("@rules_swift//swift:swift.bzl", "swift_library", "swift_interop_hint")
load("//bazel:flags.bzl", "CPP_FLAGS", "MAPLIBRE_FLAGS", "WARNING_FLAGS")

# Core C++ plugin implementation
cc_library(
    name = "arrow-polyline-example-plugin",
    srcs = [
        "src/ArrowPolylineExampleImpl.cpp",
    ] + glob([
        "generated/cpp/src/**/*.cpp",
    ]),
    hdrs = [
        "src/ArrowPolylineExampleImpl.hpp",
    ] + glob([
        "generated/cpp/include/**/*.h",
    ]),
    copts = CPP_FLAGS + MAPLIBRE_FLAGS,
    includes = [
        "generated/cpp/include",
        "src",
    ],
    visibility = ["//visibility:public"],
    deps = [
        "//:mbgl-core",
    ],
)

# Swift interop hint for the cbridge module
swift_interop_hint(
    name = "arrow-polyline-cbridge-swift-hint",
    module_map = "generated/cbridge/include/module.modulemap",
    module_name = "GluecodiumCBridge",
)

# CBridge as objc_library with swift_interop_hint
objc_library(
    name = "arrow-polyline-cbridge",
    srcs = glob(["generated/cbridge/src/**/*.cpp"]),
    hdrs = glob([
        "generated/cbridge/include/**/*.h",
        "generated/cbridge_internal/include/**/*.h",
    ]) + [
        "generated/cbridge/include/module.modulemap",
    ],
    copts = CPP_FLAGS + MAPLIBRE_FLAGS + WARNING_FLAGS["ios"] + [
        "-Wno-pragma-once-outside-header",
        "-Wno-global-constructors",
    ],
    includes = [
        "generated",  # Headers use "cbridge/include/..." and "cbridge_internal/include/..." paths
        "generated/cbridge/include",
    ],
    aspect_hints = [":arrow-polyline-cbridge-swift-hint"],
    visibility = ["//visibility:public"],
    deps = [
        ":arrow-polyline-example-plugin",
    ],
)

# iOS bridge for Gluecodium plugin integration with MLNMapView
objc_library(
    name = "arrow-polyline-ios-bridge",
    srcs = ["ios/MaplibreGluecodiumPluginBridge.mm"],
    hdrs = ["ios/MaplibreGluecodiumPluginBridge.h"],
    copts = CPP_FLAGS + MAPLIBRE_FLAGS + WARNING_FLAGS["ios"],
    module_name = "ArrowPolylineGluecodiumPluginBridge",
    visibility = ["//visibility:public"],
    deps = [
        "//platform:ios-sdk",
    ],
)

# Swift bindings generated by Gluecodium
swift_library(
    name = "arrow-polyline-swift",
    srcs = glob(["generated/swift/**/*.swift"]) + [
        "ios/CBridgeExport.swift",
    ],
    module_name = "GluecodiumArrowPolyline",
    visibility = ["//visibility:public"],
    deps = [
        ":arrow-polyline-cbridge",
    ],
)
